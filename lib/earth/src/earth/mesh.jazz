;;;==============
;;;  GaeaScheme
;;;==============
;;;
;;;; Earth Mesh
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module earth.mesh jazz


(import (jazz.application)
        (jazz.graphic)
        (jazz.io)
        (jazz.markup)
        (jazz.platform)
        (jazz.process)
        (jazz.screen)
        (gaea.array)
        (gaea.buffer)
        (gaea.generate)
        (gaea.geometry)
        (gaea.mesh)
        (gaea.mesher)
        (gaea.pipeline)
        (gaea.program)
        (gaea.program.mesh)
        (gaea.program.texture)
        (gaea.render)
        (gaea.shader)
        (gaea.texture)
        (gaea.uniform)
        (gaea.window))


;;;
;;;; Window
;;;


(class Mesh-Window extends Gaea-Window
  
  
  (form
    (<install> caption-base: "Mesh"))
  
  
  ;; Pipelines
  (slot mesh-pipeline       initialize #f getter generate)
    
  ;; Uniforms
  (slot uniform-buffer      initialize #f getter generate)

  ;; Meshes
  (slot texture-mesh        initialize #f getter generate)
    
  ;; Textures
  (slot texture             initialize #f getter generate)

  ;; Matrices
  (slot projection-matrix   initialize (make-matrix) getter generate)
  (slot view-matrix         initialize (make-matrix) getter generate)
  
  
  (method override (destroy self)
    (stop-render self)
    (nextmethod self))
  
  
  (method override (setup-engine self)
    ;; quicky until crashes fixed
    (platformInhibitAutoReleasePool)
    (setup-pipelines self)
    (setup-meshes self)
    (setup-textures self)
    (start-render self))
  
  
  (method (setup-pipelines self)
    (set! mesh-pipeline (new Pipeline (require-program 'texture) mesh-layout)))
  
  
  ;;;
  ;;;; Meshes
  ;;;
  
  
  (method (setup-meshes self)
    (define content
      #f32( 1.0 0.0 -5.0   1.0 0.0 0.0   0.0 0.0 0.0   0.0
            3.0 0.0 -5.0   1.0 0.0 0.0   1.0 0.0 0.0   0.0
            3.0 2.0 -5.0   1.0 0.0 0.0   1.0 1.0 0.0   0.0
            3.0 2.0 -5.0   1.0 0.0 0.0   0.0 0.0 0.0   0.0
            1.0 2.0 -5.0   1.0 0.0 0.0   0.0 1.0 0.0   0.0
            1.0 0.0 -5.0   1.0 0.0 0.0   1.0 1.0 0.0   0.0 ))
    
    (let ((texture-vertices (new Buffer content)))
      (set! texture-mesh (new Array @w Mesh mesh-pipeline 1 6 texture-vertices))))
  
  
  ;;;
  ;;;; Textures
  ;;;
  
  
  (method (setup-textures self)
    (set! texture (make-file-cairo-texture {File Home "Media" "Oasis.png"}))
    (flush (get-surface texture))
    (map-texture texture)
    (setup-uniform texture mesh-pipeline "texture_sampler"))
  
  
  ;;;
  ;;;; Resize
  ;;;
  
  
  (method override (resize-scene self width <fl> height <fl>)
    (nextmethod self width height)
    
    ;; Projection
    (let ((ratio (/ width height)))
      (make-perspective-matrix! projection-matrix 53.13 ratio 1.0 30.0))
    
    ;; View
    (make-view-matrix! view-matrix (vertex 7. 2. 6.) (lookat (vertex -.513 -.110 -.852) (vertex -.056 .994 -.094) (vertex .857 -4.195e-8 -.516)))
    
    ;; Uniform
    (when (not uniform-buffer)
      (let ((content (make-f32vector (* 3 16))))
        (matrix-transfer! projection-matrix content 0)
        (matrix-transfer! view-matrix content 16)
        (matrix-transfer! (make-identity-matrix) content 32)
        (set! uniform-buffer (new Uniform-Buffer "Matrices" 0 content))
        (uniform-binding mesh-pipeline uniform-buffer))))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method override (draw-scene self)
    (with-render self
      (lambda (encoder)
        (when uniform-buffer
          (bind-uniform encoder uniform-buffer 0))
        (with-pipeline mesh-pipeline encoder
          (lambda ()
            (bind-texture texture encoder)
            (draw-array texture-mesh encoder)))))))


;;;
;;;; Application
;;;


(class Mesh-Application extends Application
  
  
  (method override (new-toplevel self)
    (new Mesh-Window position: {Point 200 150} size: {Dimension 800 500})))


;;;
;;;; Run
;;;


(definition (run-mesh descriptor)
  (boot-environment (new Mesh-Application)))


(register-product-run 'earth.mesh
  run-mesh))
