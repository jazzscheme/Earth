;;;==============
;;;  GaeaScheme
;;;==============
;;;
;;;; Earth Array
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module earth.array jazz


(import (jazz.application)
        (jazz.graphic)
        (jazz.io)
        (jazz.markup)
        (jazz.platform)
        (jazz.process)
        (jazz.screen)
        (gaea.array)
        (gaea.buffer)
        (gaea.generate)
        (gaea.geometry)
        (gaea.opengl)
        (gaea.opengl.glew)
        (gaea.program)
        (gaea.program.color)
        (gaea.shader)
        (gaea.window))


;;;
;;;; Window
;;;


(class Array-Window extends Gaea-Window
  
  
  (form
    (<install> caption-base: "Array"))
  
  
  ;; Programs
  (slot color-program       initialize #f getter generate)
  
  ;; Arrays
  (slot blue-array          initialize #f getter generate)
  (slot red-array           initialize #f getter generate)
  
  ;; Matrices
  (slot projection-matrix   initialize (make-matrix) getter generate)
  (slot view-matrix         initialize (make-matrix) getter generate)
  
  ;; Render
  (slot render-thread       initialize #f getter generate)
  
  
  (method override (destroy self)
    (stop-render self)
    (nextmethod self))
  
  
  (method override (setup-engine self)
    (setup-programs self)
    (initialize-arrays self)
    (start-render self))
  
  
  (method (setup-programs self)
    (set! color-program (require-program 'color)))
  
  
  (method (start-render self)
    (let ((thread
            (new-thread
              (lambda ()
                (declare (proper-tail-calls))
                (let ((period (/ 1. 30.)))
                  (let (loop)
                    (render-scene self)
                    (sleep period)
                    (loop))))
              'render)))
      (set! render-thread thread)
      (thread-start! thread)))
  
  
  (method (stop-render self)
    (when render-thread
      (thread-terminate! render-thread)
      (set! render-thread #f)))
  
  
  ;;;
  ;;;; Arrays
  ;;;
  
  
  (method (initialize-arrays self)
    (define (array-generate data)
      (let ((array (new Color-Array)))
        (allocate-vertices array)
        (set-data (get-vertices-buffer array) data color-triangle-floats color-vertex-floats)
        (setup-array array color-program)
        array))
    
    (set! red-array (create-red self array-generate))
    (set! blue-array (create-blue self array-generate)))
  
  
  (method (create-red self generate)
    (generate
      #f32( 1.0 0.0 -5.0 1.0 0.0 0.0
            3.0 0.0 -5.0 1.0 0.0 0.0
            2.0 2.0 -5.0 1.0 0.0 0.0)))
  
  
  (method (create-blue self generate)
    (generate
      #f32( -3.0 0.0 -5.0 0.0 0.0 1.0
            -1.0 0.0 -5.0 0.0 0.0 1.0
            -2.0 2.0 -5.0 0.0 0.0 1.0)))
  
  
  ;;;
  ;;;; Resize
  ;;;
  
  
  (method override (resize-scene self width <fl> height <fl>)
    ;; Viewport
    (let ((scale-factor (main-screen-scale-factor)))
      (glViewport 0 0 (* scale-factor (fxround width)) (* scale-factor (fxround height))))
    
    ;; Projection
    (let ((ratio (/ width height)))
      (make-perspective-matrix! projection-matrix 53.13 ratio 1.0 30.0))
    
    ;; View
    (make-view-matrix! view-matrix (vertex 7. 2. 6.) (lookat (vertex -.513 -.110 -.852) (vertex -.056 .994 -.094) (vertex .857 -4.195e-8 -.516))))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method override (draw-scene self)
    ;; Background
    (glClearColor 0.0 0.0 0.0 0.0)
    (glClear (bitwise-ior GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))
    
    ;; Color
    (use color-program)
    (bind-uniforms color-program projection-matrix view-matrix)
    (draw-array red-array)
    (draw-array blue-array)
    (unbind-uniforms color-program)
    (unuse color-program)))


;;;
;;;; Application
;;;


(class Array-Application extends Application
  
  
  (method override (new-toplevel self)
    (new Array-Window position: {Point 200 150} size: {Dimension 800 500})))


;;;
;;;; Run
;;;


(definition (run-array descriptor)
  (boot-environment (new Array-Application)))


(register-product-run 'earth.array
  run-array))
