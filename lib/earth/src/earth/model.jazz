;;;==============
;;;  GaeaScheme
;;;==============
;;;
;;;; Earth Model
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module earth.model jazz


(import (jazz.application)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.io)
        (jazz.markup)
        (jazz.platform)
        (jazz.process)
        (jazz.opengl)
        (jazz.opengl.glew)
        (jazz.opengl.texture)
        (jazz.screen)
        (gaea.array)
        (gaea.buffer)
        (gaea.generate)
        (gaea.geometry)
        (gaea.mesh)
        (gaea.mesher)
        (gaea.program)
        (gaea.program.mesh)
        (gaea.program.model)
        (gaea.shader)
        (gaea.texture)
        (gaea.uv)
        (gaea.window))


;;;
;;;; Window
;;;


(class Model-Window extends Gaea-Window
  
  
  (form
    (<install> caption-base: "Model"))
  
  
  ;; Programs
  (slot mesh-program        initialize #f getter generate)
  
  ;; Meshes
  (slot model-mesh          initialize #f getter generate)
  
  ;; Matrices
  (slot projection-matrix   initialize (make-matrix) getter generate)
  (slot view-matrix         initialize (make-matrix) getter generate)
  (slot model-matrix        initialize (make-matrix) getter generate)
  
  ;; Textures
  (slot texture             initialize #f getter generate)
  (slot animate?            initialize #t getter generate)
  (slot angle               initialize -90. getter generate)
  (slot elevation           initialize 0. getter generate)
  (slot direction           initialize 1. getter generate)
  
  ;; Render
  (slot render-thread       initialize #f getter generate)
  
  
  (method override (destroy self)
    (stop-render self)
    (nextmethod self))
  
  
  (method override (setup-engine self)
    (setup-programs self)
    (initialize-meshes self)
    (start-render self))
  
  
  (method (setup-programs self)
    (set! mesh-program (require-program 'model)))
  
  
  (method (start-render self)
    (let ((thread
            (new-thread
              (lambda ()
                (declare (proper-tail-calls))
                (let ((period (/ 1. 30.)))
                  (let (loop)
                    (render-scene self)
                    (sleep period)
                    (loop))))
              'render)))
      (set! render-thread thread)
      (thread-start! thread)))
  
  
  (method (stop-render self)
    (when render-thread
      (thread-terminate! render-thread)
      (set! render-thread #f)))
  
  
  ;;;
  ;;;; Meshes
  ;;;
  
  
  (method (initialize-meshes self)
    (define (create-mesh)
      (let ((mesher (new Mesher 0 2))
            (v1 (vertex -8.0 -4.0 0.0))
            (v2 (vertex  0.0 -4.0 0.0))
            (v3 (vertex  8.0 -4.0 0.0))
            (v4 (vertex -8.0  0.0 0.0))
            (v5 (vertex  0.0  0.0 0.0))
            (v6 (vertex  8.0  0.0 0.0))
            (v7 (vertex -8.0  4.0 0.0))
            (v8 (vertex  0.0  4.0 0.0))
            (v9 (vertex  8.0  4.0 0.0))
            (normal (vertex 0. 0. 1.))
            (tl uv/left)
            (tt uv/top)
            (tr uv/right)
            (tb uv/bottom))
        (let ((uv (uv 0. 0. .5 1. 0.))
              (depth 0.))
          (add-quad mesher
                    v1 normal tl tb 0
                    v2 normal tr tb 1
                    v8 normal tr tt 1
                    v7 normal tl tt 0
                    uv
                    depth))
        (let ((uv (uv .5 0. 1. 1. 0.))
              (depth 0.))
          (add-quad mesher
                    v2 normal tl tb 1
                    v3 normal tr tb 2
                    v9 normal tr tt 2
                    v8 normal tl tt 1
                    uv
                    depth))
        (get-mesh mesher mesh-program)))
    
    (set! model-mesh (create-mesh)))
  
  
  ;;;
  ;;;; Resize
  ;;;
  
  
  (method override (resize-scene self width <fl> height <fl>)
    ;; Viewport
    (let ((scale-factor (main-screen-scale-factor)))
      (glViewport 0 0 (* scale-factor (fxround width)) (* scale-factor (fxround height))))
    
    ;; Projection
    (let ((ratio (/ width height)))
      (make-perspective-matrix! projection-matrix 53.13 ratio 1.0 30.0))
    
    ;; View
    ;(make-view-matrix! view-matrix (vertex 7. 2. 6.) (lookat (vertex -.513 -.110 -.852) (vertex -.056 .994 -.094) (vertex .857 -4.195e-8 -.516)))
    (make-view-matrix! view-matrix (vertex 0. 0. 12.) (make-standard-lookat))
    
    ;; Model
    ;(make-translation-matrix! model-matrix (vertex -2. 0. -5.))
    (make-identity-matrix! model-matrix))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method override (draw-scene self)
    ;; Background
    (glClearColor 0.0 0.0 0.0 0.0)
    (glClear (bitwise-ior GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))
    
    ;; Texture
    (when (not texture)
      (set! texture (make-file-cairo-texture {File Home "Media" "Oasis.png"}))
      (let ((surface (get-surface texture)))
        (flush surface)
        (map-texture texture)))
    
    ;; Mesh
    (use mesh-program)
    (bind-material mesh-program (get-id texture))
    (let ((projection (sqrt (- (expt 8. 2.) (expt elevation 2.)))))
      (let ((translation (- 8. projection)))
        (bind-bones mesh-program (list (make-translation-matrix (vertex translation 0. 0.))
                                       (make-translation-matrix (vertex 0. 0. (- elevation)))
                                       (make-translation-matrix (vertex (- translation) 0. 0.))))))
    @w
    (bind-bones mesh-program (list (make-identity-matrix)
                                   (make-identity-matrix)
                                   (make-y-rotation-matrix (degree->radian (between 0. angle 180.)))))
    (bind-uniforms mesh-program projection-matrix view-matrix model-matrix)
    (draw-array model-mesh)
    (unbind-uniforms mesh-program)
    (unuse mesh-program)
    
    ;; Elevation
    (when animate?
    (when (< elevation 8.)
      (set! elevation (min 8. (+ elevation .05)))))
    
    ;; Angle
    (when animate?
    (cond ((= direction 1.)
           (set! angle (+ angle 3.))
           (when (>= angle 270.)
             (set! direction -1)))
          (else
           (set! angle (- angle 3.))
           (when (<= angle -90.)
             (set! direction 1.))))))
  
  
  (method override (key-press self c)
    (set! animate? (not animate?))))


;;;
;;;; Application
;;;


(class Model-Application extends Application
  
  
  (method override (new-toplevel self)
    (new Model-Window position: {Point 200 150} size: {Dimension 800 500})))


;;;
;;;; Run
;;;


(definition (run-model descriptor)
  (boot-environment (new Model-Application)))


(register-product-run 'earth.model
  run-model))
